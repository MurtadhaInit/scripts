resource "proxmox_virtual_environment_vm" "ubuntu_template" {
  name        = "ubuntu-template"
  description = "Preconfigured template based on an Ubuntu cloud image"
  tags        = ["terraform"]
  node_name   = var.pve_hostname
  vm_id       = 500
  on_boot     = true
  started     = false
  template    = true

  memory {
    dedicated = 2048
    floating  = 0 # disables "ballooning device"
  }

  cpu {
    cores = 2
    numa  = true
    type  = "host"
    units = 1024
  }

  network_device {
    bridge = "vmbr0"
    model  = "virtio"
  }

  disk {
    datastore_id = var.pve_storage
    interface    = "scsi0" # virtio0
    discard      = "on"
    iothread     = true # for best disk performance: iothread + virtio-scsi-single
    ssd          = true
    size         = 10
    file_id      = proxmox_virtual_environment_download_file.ubuntu_cloud_image.id
    # file_format = "raw" # what will be applied anyways
  }

  # for 'Console' display
  serial_device {
    device = "socket"
  }
  vga {
    type = "std"
    # type = "serial0"
    # clipboard = "vnc"
  }

  scsi_hardware = "virtio-scsi-single"

  operating_system {
    type = "l26"
  }

  agent {
    # The qemu-guest-agent needs to be installed and running inside the VM
    enabled = true
  }

  initialization {
    # interface = "ide2" # the default interface
    ip_config {
      ipv4 {
        address = "dhcp"
      }
      ipv6 {
        address = "dhcp"
      }
    }
    user_data_file_id = proxmox_virtual_environment_file.user_data_cloud_config_regular_vms.id
    datastore_id      = var.pve_storage
  }
}

resource "proxmox_virtual_environment_file" "user_data_cloud_config_regular_vms" {
  datastore_id = var.pve_storage
  content_type = "snippets"
  node_name    = var.pve_hostname
  overwrite    = true

  source_raw {
    data = <<-EOF
    #cloud-config
    hostname: ubuntu-vm
    users:
      - name: ${var.vm_regular_username}
        lock_passwd: false
        passwd: ${random_password.ubuntu_template_pass.bcrypt_hash}
        groups:
          - sudo
        shell: /bin/bash
        ssh_authorized_keys:
          - ${trimspace(file(var.vm_ssh_public_key))}
      - name: automator
        gecos: Automation User
        lock_passwd: true
        groups:
          - sudo
        sudo: ALL=(ALL) NOPASSWD:ALL
        shell: /bin/bash
        ssh_authorized_keys:
          - ${trimspace(file(var.vm_ssh_public_key))}
    packages:
        - qemu-guest-agent
        - net-tools
    runcmd:
      - systemctl enable qemu-guest-agent
      - systemctl start qemu-guest-agent
      - echo "done" > /tmp/cloud-config.done
    package_update: true
    package_upgrade: true
    package_reboot_if_required: true
    disable_root: true
    ssh_pwauth: false
    EOF

    file_name = "user-data-cloud-config-regular-vms.yaml"
  }
}

resource "random_password" "ubuntu_template_pass" {
  length           = 16
  special          = true
  override_special = "_%@"
}

output "ubuntu_template_pass" {
  value     = random_password.ubuntu_template_pass.result
  sensitive = true
}
